# 08 Get Alzheimer disease type 1(AD1) related genes whose expression levels are 99.7 or higher.
PREFIX brso: <http://purl.jp/bio/10/brso/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX genex: <http://purl.org/genex#>
PREFIX umls: <http://linkedlifedata.com/resource/umls/id/>
PREFIX medgen: <http://www.ncbi.nlm.nih.gov/medgen/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?idntf_ncbi1 ?ensembl1 ?anatomy ?label_anatomy ?exprLevel
WHERE {
#	GRAPH <http://metadb.riken.jp/db/mim2gene_medgen_disease> {
		?medgen <http://med2rdf/ontology/medgen#mgconso> ?bL_disease.
			VALUES (?medgen) { (medgen:C1863052) } # Alzheimer disease type 1(AD1)
		?idntf_ncbi1 obo:RO_0003302 ?medgen. 

#	GRAPH <http://metadb.riken.jp/db/ensemblRDF> {
		?ensembl1 rdfs:seeAlso ?idntf_ncbi1.
		FILTER CONTAINS(STR(?idntf_ncbi1), "http://identifiers.org/ncbigene/")
		FILTER CONTAINS(STR(?ensembl1), "http://rdf.ebi.ac.uk/resource/ensembl/")

#	GRAPH <http://metadb.riken.jp/db/bgee> {
		?oma_gene1 a orth:Gene .
		?oma_gene1 lscr:xrefEnsemblGene ?ensembl1 .
		?oma_gene1 orth:organism/obo:RO_0002162 taxon:9606 . # human
		?oma_gene1 genex:isExpressedIn ?cond .
		?cond genex:hasAnatomicalEntity ?anatomy. #obo:UBERON_0000451 . # prefrontal cortex
			FILTER CONTAINS(STR(?anatomy),"http://purl.obolibrary.org/obo/UBERON_")
			FILTER (datatype(?label_anatomy) = xsd:string)
		?anatomy rdfs:label ?label_anatomy.
		?expr genex:hasExpressionCondition ?cond .
		?expr genex:hasSequenceUnit ?oma_gene1 .
		?expr a genex:Expression .
		?expr genex:hasConfidenceLevel obo:CIO_0000029 . # high confidence level
		?expr genex:hasExpressionLevel ?exprLevel .
			FILTER (?exprLevel > 99.7)
}
ORDER BY desc(?exprLevel)
